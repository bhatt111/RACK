uri "http://arcos.rack/TESTING" alias tst.
import "http://arcos.rack/PROV-S".

TEST
	(note "Description of a single test")
	is a type of ENTITY.

	verifies (note "Parent ENTITY(s) (e.g. REQUIREMENT) that this requirement is derived from") describes TEST with values of type ENTITY.
	verifies is a type of wasDerivedFrom.

	producedBy (note "TEST_DEVELOPMENT activity(s) this test belongs to") describes TEST with values of type ACTIVITY.
	producedBy is a type of wasGeneratedBy.

TEST_RESULT
	(note "Result corresponding to one execution of one TEST")
	is a type of ENTITY.

	result (note "Pass/Failed/Indeterminate status resulting from a TEST_EXECUTION") describes TEST_RESULT with values of type TEST_STATUS.
	TEST_STATUS is a class, must be one of {Passed, Failed, Indeterminate}.

	confirms (note "Parent test(s) of this test result") describes TEST_RESULT with values of type ENTITY.
	confirms is a type of wasDerivedFrom.

	executedBy (note "ACTIVITY(s) that created this test result") describes TEST_RESULT with values of type ACTIVITY.
	executedBy is a type of wasGeneratedBy.

TEST_DEVELOPMENT
	(note "ACTIVITY of creating a TEST to participate in the evaluation of a requirement")
	is a type of ACTIVITY.

	developedBy (note "Entity that is responsible for producing a test, could be a person or a tool. ") describes TEST_DEVELOPMENT with values of type AGENT.
	developedBy is a type of wasAssociatedWith.

TEST_EXECUTION
	(note "ACTIVITY of performing a TEST and record its corresponding TEST_RESULT")
	is a type of ACTIVITY.

	executedOn describes TEST_EXECUTION with values of type AGENT.
	executedOn (note "AGENT(s) (e.g. some testing software/machine) running those tests").
	executedOn is a type of wasAssociatedWith.
