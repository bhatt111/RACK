/* Copyright (c) 2020, General Electric Company, Galois, Inc.
 *
 * All Rights Reserved
 *
 * This material is based upon work supported by the Defense Advanced Research
 * Projects Agency (DARPA) under Contract No. FA8750-20-C-0203.
 *
 * Any opinions, findings and conclusions or recommendations expressed in this
 * material are those of the author(s) and do not necessarily reflect the views
 * of the Defense Advanced Research Projects Agency (DARPA).
 */

/************** edit history *****************
  *
  * dwa - 11 Nov 2020 - reduced cardinality of several
  * dwa - 11 Nov 2020 - removed incorrect associations
  * dwa - 11 Nov 2020 - removed unnecessary subclassing
  *
  *********************************************/

uri "http://arcos.rack/REQUIREMENTS" alias Rq.
import "http://arcos.rack/PROV-S".
import "http://arcos.rack/HAZARD".
import "http://arcos.rack/SYSTEM".
import "http://arcos.rack/SOFTWARE".
import "http://arcos.rack/TESTING".
import "http://arcos.rack/REVIEW".

REQUIREMENT
	(note "Captures (both high- and low-level) properties of a process or artifact that are to be assessed")
	is a type of ENTITY.

// dwa - reduce cardinality to 1:1
	text (note "Description of the REQUIREMENT") describes REQUIREMENT with a single value of type string.
	// The "text" string can optionally be subdivided into sub-parts : givenText, ifText, thenText
	// This corresponds to how rules are defined in SADL and note that in a SADL rule whatever is in
	// givenText can simply be included in the ifText part.
	// Note that modulo naming changes this structure also matches the structure
	// that GrammaTech had which was : Given, When, Then parts.
	// The structure of requirements in ASSERT subsume this structure of requirements. 
	givenText (note "optional part that can be used to set certain conditions apart from other conditions in the ifText part") describes REQUIREMENT with values of type string.
	givenText describes REQUIREMENT with at most 1 value.
	ifText (note "conditions under which this requirement holds") describes REQUIREMENT with values of type string.
	ifText describes REQUIREMENT with at most 1 value.
	thenText (note "specifies what should hold by this requirement if the requirement conditions are met") describes REQUIREMENT with values of type string.
	thenText describes REQUIREMENT with at least 1 value.

// dwa - should be refined to SYSTEM, COMPONENT, or TEST, I think
	governs (note "ENTITY(s) that are the subject of the requirement") describes REQUIREMENT with values of type ENTITY.
// dwa - governs is NOT a type of wasDerivedFrom - remove
//	governs is a type of wasDerivedFrom.

	satisfies (note "Parent ENTITY(s) (e.g. REQUIREMENT) that this REQUIREMENT is derived from") describes REQUIREMENT with values of type ENTITY.
// dwa - satisfies is NOT a type of wasDerivedFrom - remove
//	satisfies is a type of wasDerivedFrom.

// dwa - refine to HAZARD, since mitigates doesn't seem to have any other reasonable objects
	Rq:mitigates (note "ENTITY(s) (e.g. HAZARD) that is being mitigated by this REQUIREMENT") describes REQUIREMENT with values of type HAZARD.
// dwa - mitigates is NOT a type of wasDerivedFrom - remove
//	mitigates is a type of wasDerivedFrom.

	Rq:createdBy (note "ACTIVITY(s) (e.g. REQUIREMENT_DEVELOPMENT) that created this REQUIREMENT") describes REQUIREMENT with values of type ACTIVITY.
	Rq:createdBy is a type of wasGeneratedBy.

 DATA_DICTIONARY_TERM
	(note "Defines some conceptual data that may be collected somewhere and used somewhere else")
	is a type of ENTITY.

	text (note "Description of the term") describes DATA_DICTIONARY_TERM with values of type string.
	text describes DATA_DICTIONARY_TERM with at most 1 value.

	providedBy (note "ENTITY(s) (e.g. REQUIREMENT) that provides this information") describes DATA_DICTIONARY_TERM with values of type ENTITY.
//	providedBy is a type of wasDerivedFrom.

	consumedBy (note "ENTITY(s) (e.g. REQUIREMENT) that consumes this information") describes DATA_DICTIONARY_TERM with values of type ENTITY.
//	consumedBy is a type of wasDerivedFrom.

	Rq:createdBy (note "ACTIVITY(s) (e.g. REQUIREMENT_DEVELOPMENT) that created this data dictionary term") describes DATA_DICTIONARY_TERM with values of type ACTIVITY.
	Rq:createdBy is a type of wasGeneratedBy.


REQUIREMENT_DEVELOPMENT
	(note "ACTIVITY that produces REQUIREMENTs")
	is a type of ACTIVITY.

	Rq:author (note "AGENT(s) responsible for producing a REQUIREMENT") describes REQUIREMENT_DEVELOPMENT with values of type AGENT.
	Rq:author is a type of wasAssociatedWith.

	Rq:referenced (note "Source materials (requirement and design docs) used during development") describes REQUIREMENT_DEVELOPMENT with values of type ENTITY.
	Rq:referenced is a type of used.

	Rq:governedBy (note "ENTITY(s) (e.g. REQUIREMENTS, any sort of document) used during this ACTIVITY") describes REQUIREMENT_DEVELOPMENT with values of type ENTITY.
	Rq:governedBy is a type of used.
